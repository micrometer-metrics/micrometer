plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.18'
}

apply plugin: 'io.spring.dependency-management'

repositories {
    mavenCentral()
}

configurations {
    implementation {
        // see gh-1599; pause detection is not configured so this dependency can be excluded
        exclude module: 'LatencyUtils'
    }
}

dependencies {
    implementation project(":micrometer-core")
    ['atlas', 'azure-monitor', 'prometheus-simpleclient', 'datadog', 'elastic', 'ganglia', 'graphite', 'health', 'jmx', 'influx', 'statsd', 'new-relic', 'cloudwatch2', 'signalfx', 'wavefront', 'elastic', 'dynatrace', 'humio', 'appoptics', 'stackdriver'].each { sys ->
        implementation project(":micrometer-registry-$sys")
    }

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation('org.springframework.boot:spring-boot-starter-actuator') {
        exclude group: 'io.micrometer', module: 'micrometer-core'
    }

    implementation platform(libs.spring.cloud)
    implementation 'org.springframework.cloud:spring-cloud-starter-sleuth'
    implementation 'org.springframework.cloud:spring-cloud-sleuth-zipkin'

}

configurations.all {
    // see: https://github.com/micrometer-metrics/micrometer/issues/3954
    exclude group: 'com.google.guava', module: 'listenablefuture'
}

bootJar {
    enabled = false
}

bootJarMainClassName {
    enabled = false
}
