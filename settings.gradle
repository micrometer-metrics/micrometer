pluginManagement {
    repositories {
        gradlePluginPortal()
        maven { url 'https://repo.spring.io/release' }
    }
}

plugins {
    id 'com.gradle.enterprise' version '3.6.3'
    id 'io.spring.ge.conventions' version '0.0.7'
}

rootProject.name = 'micrometer'

buildCache {
    local {
        enabled = true
    }
    remote(HttpBuildCache) {
        enabled = false
        url = 'https://ge.micrometer.io/cache/'
    }
}
gradleEnterprise {
    server = 'https://ge.micrometer.io'
    buildScan {
        if (System.env.'CIRCLE_BUILD_URL') {
            link 'circleci', System.env.'CIRCLE_BUILD_URL'
        }
    }
}

include 'micrometer-core'
include 'micrometer-jersey2'

['core', 'boot2', 'boot2-reactive', 'spring-integration', 'hazelcast', 'hazelcast3', 'javalin'].each { sample ->
    include "micrometer-samples-$sample"
    project(":micrometer-samples-$sample").projectDir = new File(rootProject.projectDir, "samples/micrometer-samples-$sample")
}

include 'micrometer-test'

['atlas', 'prometheus', 'datadog', 'elastic', 'ganglia', 'graphite', 'health', 'jmx', 'influx', 'statsd', 'new-relic', 'cloudwatch', 'cloudwatch2', 'signalfx', 'wavefront', 'dynatrace', 'azure-monitor', 'humio', 'appoptics', 'kairos', 'stackdriver', 'opentsdb'].each { sys ->
    include "micrometer-registry-$sys"
    project(":micrometer-registry-$sys").projectDir = new File(rootProject.projectDir, "implementations/micrometer-registry-$sys")
}

['core'].each { benchmark ->
    include "micrometer-benchmarks-$benchmark"
    project(":micrometer-benchmarks-$benchmark").projectDir = new File(rootProject.projectDir, "benchmarks/benchmarks-$benchmark")
}

include 'micrometer-bom'
