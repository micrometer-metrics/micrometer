pluginManagement {
    repositories {
        gradlePluginPortal()
    }
}

plugins {
    id 'com.gradle.enterprise' version '3.16.2'
    id 'io.spring.ge.conventions' version '0.0.15'
    id 'org.gradle.toolchains.foojay-resolver-convention' version '0.8.0'
}

rootProject.name = 'micrometer'

enableFeaturePreview("STABLE_CONFIGURATION_CACHE")

buildCache {
    remote(HttpBuildCache) {
        url = 'https://ge.micrometer.io/cache/'
    }
}
gradleEnterprise {
    server = 'https://ge.micrometer.io'
}

include 'micrometer-commons', 'micrometer-core', 'micrometer-observation'

['core', 'boot2', 'boot2-reactive', 'spring-integration', 'hazelcast', 'hazelcast3', 'javalin', 'jersey3', 'jetty12', 'jooq', 'kotlin'].each { sample ->
    include "micrometer-samples-$sample"
    project(":micrometer-samples-$sample").projectDir = new File(rootProject.projectDir, "samples/micrometer-samples-$sample")
}

include 'micrometer-test', 'micrometer-observation-test'

['atlas', 'prometheus', 'datadog', 'elastic', 'ganglia', 'graphite', 'health', 'jmx', 'influx', 'otlp', 'statsd', 'new-relic', 'cloudwatch', 'cloudwatch2', 'signalfx', 'wavefront', 'dynatrace', 'azure-monitor', 'humio', 'appoptics', 'kairos', 'stackdriver', 'opentsdb'].each { sys ->
    include "micrometer-registry-$sys"
    project(":micrometer-registry-$sys").projectDir = new File(rootProject.projectDir, "implementations/micrometer-registry-$sys")
}

['core'].each { benchmark ->
    include "micrometer-benchmarks-$benchmark"
    project(":micrometer-benchmarks-$benchmark").projectDir = new File(rootProject.projectDir, "benchmarks/benchmarks-$benchmark")
}

include 'micrometer-bom'
include 'micrometer-jakarta9'
include 'micrometer-jetty11'
include 'micrometer-osgi-test'
include 'docs'
