buildscript {
    repositories {
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
        mavenLocal()
    }
    dependencies {
        classpath 'org.ow2.asm:asm:5.0.3'
        classpath 'io.spring.gradle:spring-release-plugin:0.11.0'
        classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:3.2.0'
        classpath 'me.champeau.gradle:jmh-gradle-plugin:0.4.4'
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0'
        classpath 'com.netflix.nebula:nebula-project-plugin:3.4.0'
    }
}

allprojects {
    apply plugin: 'io.spring.release'
    afterEvaluate { project ->
        println "I'm building $project.name with version $project.version"
    }
    group = 'io.micrometer'
}

subprojects {
    apply plugin: 'java'

    description = 'Application monitoring instrumentation facade'

    contacts {
        'jschneider@pivotal.io' {
            moniker 'Jon Schneider'
            github 'jkschneider'
        }
    }

    repositories {
        mavenCentral()
    }

    if (project.extensions.findByName('bintray')) {
        bintray.pkg {
            labels = ['micrometer', 'atlas', 'metrics', 'prometheus', 'spectator', 'influx']
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.0.1'
}
