buildscript {
    repositories {
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
        maven {
            url 'https://repo.spring.io/plugins-release'
        }
        mavenLocal()
    }
    dependencies {
        classpath 'org.ow2.asm:asm:5.0.3'
        classpath 'io.spring.gradle:spring-release-plugin:0.20.1'
        classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:3.2.0'
        classpath 'me.champeau.gradle:jmh-gradle-plugin:0.4.4'
        classpath 'com.netflix.nebula:nebula-project-plugin:3.4.0'
        classpath "gradle.plugin.com.dorongold.plugins:task-tree:1.3"
        classpath "io.spring.nohttp:nohttp-gradle:0.0.4.RELEASE"
    }

    configurations.classpath.resolutionStrategy.cacheDynamicVersionsFor 0, 'minutes'
}

apply plugin: 'io.spring.release'

allprojects {
    apply plugin: 'io.spring.license'
    apply plugin: 'com.dorongold.task-tree'

    afterEvaluate { project ->
        println "I'm building $project.name with version $project.version"
    }
    group = 'io.micrometer'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'io.spring.nohttp'

    dependencyLocking {
        lockAllConfigurations()
    }

    task resolveAndLockAll {
        doFirst {
            assert gradle.startParameter.writeDependencyLocks
        }
        doLast {
            def skip = ['annotationProcessor', 'apt', 'testAnnotationProcessor', 'checkstyle',
                'shadow', 'testApt', 'archives', 'junitPlatform']
            configurations.each {
                if (it.canBeResolved && !skip.contains(it.name)) {
                    it.resolve()
                }
            }
        }
    }

    compileJava.options.encoding = 'UTF-8'
    compileJava.options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
    compileTestJava.options.encoding = 'UTF-8'
    compileTestJava.options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'

    if(!project.name.contains('samples') && !project.name.contains('benchmarks')) {
        apply plugin: 'io.spring.publishing'

        contacts {
            'tludwig@vmware.com' {
                moniker 'Tommy Ludwig'
                github 'shakuzen'
            }
        }

        if (project.extensions.findByName('bintray')) {
            bintray.labels = ['micrometer', 'atlas', 'metrics', 'prometheus', 'spectator', 'influx', 'new-relic', 'signalfx', 'wavefront', 'elastic', 'dynatrace', 'azure-monitor', 'appoptics', 'kairos', 'stackdriver']
            bintray.packageName = 'io.micrometer'
        }
    }

    description = 'Application monitoring instrumentation facade'

    repositories {
        mavenCentral()
        mavenLocal()
    }

    checkstyle {
        toolVersion = '8.23'
        configFile = rootProject.file('config/checkstyle/checkstyle.xml')
    }

    def check = tasks.findByName('check')
    if (check) project.rootProject.tasks.releaseCheck.dependsOn check

    //noinspection GroovyAssignabilityCheck
    test {
        // set heap size for the test JVM(s)
        maxHeapSize = "1500m"

        useJUnitPlatform()
    }

    license {
        ext.year = Calendar.getInstance().get(Calendar.YEAR)
        skipExistingHeaders = true
    }
}

wrapper {
    gradleVersion = '4.10.2'
}

defaultTasks 'build'
