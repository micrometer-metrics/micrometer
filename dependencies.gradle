def VERSIONS = [
        'ch.qos.logback:logback-classic:1.2.+',
        'colt:colt:1.2.0',
        'com.amazonaws:aws-java-sdk-cloudwatch:latest.release',
        'com.dynatrace.metric.util:dynatrace-metric-utils-java:latest.release',
        'com.fasterxml.jackson.core:jackson-databind:latest.release',
        'com.github.ben-manes.caffeine:caffeine:2.+',
        'com.github.charithe:kafka-junit:latest.release',
        // pinned to avoid issues with shaded slf4j version - see gh-3414
        'com.github.tomakehurst:wiremock-jre8-standalone:2.33.+',
        'com.google.dagger:dagger:2.11',
        'com.google.dagger:dagger-compiler:2.11',
        'com.google.guava:guava:latest.release',
        'com.google.inject:guice:5.+',
        'com.hazelcast:hazelcast:latest.release',
        'com.h2database:h2:latest.release',
        'com.microsoft.azure:applicationinsights-core:2.+',
        // metrics are better with https://github.com/Netflix/Hystrix/pull/1568 introduced
        // in hystrix 1.5.12, but Netflix re-released 1.5.11 as 1.5.18 late in 2018.
        // <=1.5.11 or 1.5.18 doesn't break with Micrometer, but open metrics won't be correct necessarily.
        'com.netflix.hystrix:hystrix-core:1.5.12',
        'com.netflix.spectator:spectator-reg-atlas:latest.release',
        'com.newrelic.agent.java:newrelic-api:5.+',
        'com.signalfx.public:signalfx-java:latest.release',
        'com.squareup.okhttp3:okhttp:latest.release',
        'com.tngtech.archunit:archunit-junit5:latest.release',
        'com.wavefront:wavefront-sdk-java:latest.release',
        'io.dropwizard.metrics:metrics-core:4.+',
        'io.dropwizard.metrics:metrics-graphite:4.+',
        'io.dropwizard.metrics:metrics-jmx:4.+',
        // all io.grpc libraries must use the same version
        'io.grpc:grpc-api:latest.release',
        'io.grpc:grpc-core:latest.release',
        'io.grpc:grpc-services:latest.release',
        'io.grpc:grpc-stubs:latest.release',
        'io.grpc:grpc-alts:latest.release',
        'io.grpc:grpc-testing-proto:latest.release',
        'info.ganglia.gmetric4j:gmetric4j:latest.release',
        'io.prometheus:simpleclient_common:latest.release',
        'io.prometheus:simpleclient_pushgateway:latest.release',
        'io.rest-assured:rest-assured:latest.release',
        'jakarta.jms:jakarta.jms-api:3.0.+',
        'javax.cache:cache-api:latest.release',
        'javax.inject:javax.inject:1',
        'javax.servlet:javax.servlet-api:latest.release',
        'javax.xml.bind:jaxb-api:2.3.+',
        'io.micrometer:context-propagation:1.1.+',
        'org.jetbrains.kotlinx:kotlinx-coroutines-core:latest.release',
        'net.sf.ehcache:ehcache:latest.release',
        'org.apache.activemq:artemis-junit-5:latest.release',
        'org.apache.activemq:artemis-jakarta-client:latest.release',
        'org.apache.felix:org.apache.felix.framework:7.0.5',
        'org.apache.felix:org.apache.felix.scr:2.2.2',
        'org.apache.httpcomponents:httpasyncclient:latest.release',
        'org.apache.httpcomponents:httpclient:latest.release',
        'org.apache.httpcomponents.client5:httpclient5:latest.release',
        'org.apache.kafka:kafka-clients:2.+',
        'org.apache.kafka:kafka-streams:2.+',
        'org.apache.logging.log4j:log4j-core:2.+',
        'org.apache.tomcat.embed:tomcat-embed-core:8.+',
        'org.aspectj:aspectjweaver:1.8.+',
        'org.assertj:assertj-core:latest.release',
        'org.awaitility:awaitility:latest.release',
        'org.eclipse.jetty:jetty-client:9.+',
        'org.eclipse.jetty:jetty-server:9.+',
        'org.eclipse.jetty:jetty-servlet:9.+',
        'org.ehcache:ehcache:latest.release',
        'org.glassfish.jersey.core:jersey-server:2.+',
        'org.glassfish.jersey.inject:jersey-hk2:2.+',
        'org.glassfish.jersey.test-framework.providers:jersey-test-framework-provider-inmemory:2.+',
        'org.hdrhistogram:HdrHistogram:latest.release',
        'org.hibernate:hibernate-entitymanager:5.+',
        'org.hsqldb:hsqldb:2.5.+', // 2.6.0 requires JDK 11.
        'org.jooq:jooq:3.14.+',
        'org.jsr107.ri:cache-ri-impl:1.0.0',
        'org.latencyutils:LatencyUtils:latest.release',
        'org.mockito:mockito-core:5.+', // 5.x requires JDK 11.
        'org.mockito:mockito-inline:5.+',
        'org.mongodb:mongodb-driver-sync:latest.release',
        'org.osgi:org.osgi.test.junit5:1.2.1',
        'org.postgresql:postgresql:latest.release',
        'org.slf4j:slf4j-api:1.7.+',
        'org.slf4j:slf4j-simple:1.7.+',
        'org.springframework:spring-context:5.+',
        'org.springframework:spring-core:5+',
        'org.testcontainers:junit-jupiter:latest.release',
        'org.testcontainers:kafka:latest.release',
        'org.testcontainers:postgresql:latest.release',
        'org.testcontainers:mongodb:latest.release',
        'org.testcontainers:testcontainers:latest.release',
        'ru.lanwen.wiremock:wiremock-junit5:latest.release',
        'software.amazon.awssdk:cloudwatch:latest.release'
]

def PLATFORM_BOMS = [
        'io.projectreactor:reactor-bom:2020.0.+',
        'io.netty:netty-bom:4.1.+',
        'org.junit:junit-bom:5.10.+', // Not using latest.release to avoid using a milestone version.
]

subprojects {
  plugins.withId('java-library') {
    dependencies {
      constraints {
        // Direct dependencies
        VERSIONS.each {version->
          // java-library plugin has three root configurations, so we apply constraints too all of
          // them so they all can use the managed versions.
          api version
          compileOnly version
          runtimeOnly version
        }
      }
      PLATFORM_BOMS.each {bom ->
        api platform(bom)
        compileOnly platform(bom)
        runtimeOnly platform(bom)
      }
    }
  }
  configurations.configureEach {
    // see https://github.com/google/guava/releases/tag/v32.1.0
    // "Reporting dependencies that overlap with Guava"
    resolutionStrategy.capabilitiesResolution.withCapability('com.google.guava:listenablefuture') {
        select('com.google.guava:guava:0')
    }
  }
}
