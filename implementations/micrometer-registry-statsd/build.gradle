plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

dependencies {
    api project(':micrometer-core')

    implementation 'io.projectreactor:reactor-core'
    implementation 'io.projectreactor.netty:reactor-netty-core'

    testImplementation project(':micrometer-test')
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'ch.qos.logback:logback-classic'
    testImplementation 'org.awaitility:awaitility'
}

shadowJar {
    configurations = [project.configurations.compileClasspath]
    archiveClassifier = null
    dependencies {
        include(dependency('io.projectreactor:'))
        include(dependency('io.projectreactor.netty:'))
        include(dependency('org.reactivestreams:reactive-streams'))
        include(dependency('io.netty:'))
    }
    relocate 'reactor', 'io.micrometer.shaded.reactor'
    relocate 'org.reactivestreams', 'io.micrometer.shaded.org.reactorstreams'
    relocate 'io.netty', 'io.micrometer.shaded.io.netty'
    exclude 'META-INF/native/libnetty_transport_native_epoll_x86_64.so'
    metaInf {
        from "$rootDir/LICENSE"
        from "$rootDir/NOTICE"
    }
}

// Nebula plugin automatically configures the publication of the normal jar with its dependencies
// and it's not possible to reconfigure this behavior to allow using shadow's MavenPublication
// configuration, so we have to manually clean up the POM that nebula sets up instead.
jar.enabled = false
jar.dependsOn shadowJar
publishing {
    publications {
        withType(MavenPublication) {
            artifact(shadowJar)

            pom.withXml {
                asNode()
                        .dependencies
                        .dependency
                        .findAll {
                    ['reactor-core', 'reactor-netty-core'].contains(it.artifactId.text())
                }
                .each { it.parent().remove(it) }
            }
        }
    }
}
