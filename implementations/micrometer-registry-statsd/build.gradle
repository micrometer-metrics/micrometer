plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

dependencies {
    api project(':micrometer-core')

    implementation 'io.projectreactor:reactor-core'
    implementation('io.projectreactor.netty:reactor-netty-core') {
        // We do not use HTTP modules; exclude to avoid false positive CVE reports
        exclude module: 'netty-codec-http'
    }
    // We need to make sure netty dns resolution works on all macs
    implementation(group: 'io.netty', name: 'netty-resolver-dns-native-macos', classifier: 'osx-x86_64')
    implementation(group: 'io.netty', name: 'netty-resolver-dns-native-macos', classifier: 'osx-aarch_64')
    implementation(group: 'io.netty', name: 'netty-transport-native-epoll', classifier: 'linux-aarch_64')
    implementation(group: 'io.netty', name: 'netty-transport-native-epoll', classifier: 'linux-x86_64')

    testImplementation project(':micrometer-test')
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'ch.qos.logback:logback-classic'
    testImplementation 'org.awaitility:awaitility'
    // for running tests with UDS on OSX
    testImplementation(group: 'io.netty', name: 'netty-transport-native-kqueue', classifier: 'osx-x86_64')
    testImplementation(group: 'io.netty', name: 'netty-transport-native-kqueue', classifier: 'osx-aarch_64')
}

nebulaPublishVerification {
    // Verification fails due to the `+` version, but since this is a test dependency we don't publish, ignore it.
    ignore('io.netty:netty-transport-native-kqueue')
}

shadowJar {
    configurations = [project.configurations.compileClasspath]
    archiveClassifier.set('')
    dependencies {
        include(dependency('io.projectreactor:'))
        include(dependency('io.projectreactor.netty:'))
        include(dependency('org.reactivestreams:reactive-streams'))
        include(dependency('io.netty:'))
    }
    relocate 'reactor', 'io.micrometer.shaded.reactor'
    relocate 'org.reactivestreams', 'io.micrometer.shaded.org.reactorstreams'
    relocate 'io.netty', 'io.micrometer.shaded.io.netty'
    relocate 'META-INF/native/libnetty', 'META-INF/native/libio_micrometer_shaded_netty'
    metaInf {
        from "$rootDir/LICENSE"
        from "$rootDir/NOTICE"
    }
    mergeServiceFiles()
}

tasks.japicmp.dependsOn(tasks.shadowJar)

// The Nebula Publishing plugin automatically configures the publication of the shadow jar but
// shadow dependencies are added to the POM as runtime so we manually clean up the POM.
publishing {
    publications {
        withType(MavenPublication) {
            pom.withXml {
                asNode()
                    .dependencies
                    .dependency
                    .findAll { ['reactor-core', 'reactor-netty-core', 'netty-resolver-dns-native-macos', 'netty-transport-native-epoll'].contains(it.artifactId.text()) }
                    .each { it.parent().remove(it) }
            }
        }
    }
}
