plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

dependencies {
    api project(':micrometer-core')

    implementation 'io.projectreactor:reactor-core'
    implementation('io.projectreactor.netty:reactor-netty-core') {
        // We do not use HTTP modules; exclude to avoid false positive CVE reports
        exclude module: 'netty-codec-http'
    }

    testImplementation project(':micrometer-test')
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'ch.qos.logback:logback-classic'
    testImplementation 'org.awaitility:awaitility'
    // for running tests with UDS on OSX
    testImplementation 'io.netty:netty-transport-native-kqueue:+:osx-x86_64'
    testImplementation 'io.netty:netty-transport-native-kqueue:+:osx-aarch_64'
}

nebulaPublishVerification {
    // Verification fails due to the `+` version, but since this is a test dependency we don't publish, ignore it.
    ignore('io.netty:netty-transport-native-kqueue')
}

shadowJar {
    configurations = [project.configurations.compileClasspath]
    archiveClassifier.set('')
    dependencies {
        include(dependency('io.projectreactor:'))
        include(dependency('io.projectreactor.netty:'))
        include(dependency('org.reactivestreams:reactive-streams'))
        include(dependency('io.netty:'))
    }
    relocate 'reactor', 'io.micrometer.shaded.reactor'
    relocate 'org.reactivestreams', 'io.micrometer.shaded.org.reactorstreams'
    relocate 'io.netty', 'io.micrometer.shaded.io.netty'
    relocate 'META-INF/native/libnetty', 'META-INF/native/libio_micrometer_shaded_netty'
    metaInf {
        from "$rootDir/LICENSE"
        from "$rootDir/NOTICE"
    }
}

// The Nebula Publishing plugin automatically configures the publication of the shadow jar but
// shadow dependencies are added to the POM as runtime so we manually clean up the POM.
publishing {
    publications {
        withType(MavenPublication) {
            pom.withXml {
                asNode()
                    .dependencies
                    .dependency
                    .findAll { ['reactor-core', 'reactor-netty-core'].contains(it.artifactId.text()) }
                    .each { it.parent().remove(it) }
            }
        }
    }
}

tasks.japicmp.enabled = false
