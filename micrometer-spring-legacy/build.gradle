plugins {
    id "io.spring.dependency-management" version "1.0.3.RELEASE"
}

apply plugin: 'nebula.optional-base'
apply plugin: 'nebula.facet'

dependencyManagement {
    imports {
        mavenBom 'org.springframework.boot:spring-boot-dependencies:1.5.4.RELEASE'
    }
    dependencies {
        dependency 'org.assertj:assertj-core:3.8.0'
        dependency 'io.projectreactor:reactor-core:3.1.0.RELEASE'
    }
}

facets {
    samples
}

repositories {
    // for spring-cloud-test-support
    maven { url 'https://repo.spring.io/libs-milestone' }
}

dependencies {
    compile project(':micrometer-core')

    compile 'org.springframework:spring-beans'
    compile 'org.springframework.boot:spring-boot'
    compile 'org.springframework.boot:spring-boot-actuator', optional
    compile 'org.springframework.boot:spring-boot-starter-aop', optional
    compile 'org.springframework:spring-webmvc'
    compile 'org.springframework:spring-web'
    compile group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.9.0'

    compile 'javax.servlet:javax.servlet-api:3.1.0', optional
    compile 'org.aspectj:aspectjweaver:1.8.+', optional

    ['atlas', 'prometheus', 'datadog', 'ganglia', 'graphite', 'jmx', 'influx', 'statsd'].each { sys ->
        compile project(":micrometer-registry-$sys"), optional
    }

    compile 'org.springframework.integration:spring-integration-core', optional

    // to validate auto-completion on configuration properties
    compileOnly 'org.springframework.boot:spring-boot-configuration-processor'

    testCompile 'org.springframework.boot:spring-boot-starter-web'
    testCompile 'org.springframework.boot:spring-boot-actuator'

    testCompile 'junit:junit:4.+'
    testCompile 'org.springframework.boot:spring-boot-starter-test'

    // for testing DataSource metrics
    testCompile 'org.hsqldb:hsqldb'
    testCompile 'org.springframework.boot:spring-boot-starter-jdbc'
    testCompile 'com.zaxxer:HikariCP'

    // for use in mock servers
    testCompile 'org.apache.tomcat.embed:tomcat-embed-core:8.5.15'

    // for classpath exclusion in tests
    testCompile 'org.springframework.cloud:spring-cloud-test-support:2.+'

    testRuntime 'org.apache.httpcomponents:httpclient:4.4+'

    testCompile 'org.springframework.integration:spring-integration-java-dsl'
    testCompile 'org.springframework.integration:spring-integration-ws'
    testCompile 'org.springframework.integration:spring-integration-xml'

    samplesCompile 'colt:colt:1.2.0'
    samplesCompile 'org.springframework.boot:spring-boot-starter-web'
    samplesCompile 'org.springframework.boot:spring-boot-actuator'
    samplesCompile 'org.springframework.integration:spring-integration-java-dsl'
    samplesCompile 'org.springframework.integration:spring-integration-ws'
    samplesCompile 'org.springframework.integration:spring-integration-xml'
}
