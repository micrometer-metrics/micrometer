plugins {
    id 'java-platform'
}

description 'Micrometer BOM (Bill of Materials) for managing Micrometer artifact versions'

dependencies {
    constraints {
        rootProject.subprojects.findAll {
            !it.name.contains('sample') &&
                    !it.name.contains('benchmark') &&
                    !it.name.contains('concurrency-tests') &&
                    !it.name.contains('micrometer-bom') &&
                    !it.name.contains('micrometer-osgi-test') &&
                    !it.name.contains('-test-aspectj') &&
                    it.name != 'docs'
        }.each {
            api(group: it.group,
                    name: it.name,
                    version: it.version.toString())
        }
        api libs.contextPropagation
    }
}

publishing {
    publications {
        nebula(MavenPublication) {
            from components.javaPlatform
        }
    }
}
